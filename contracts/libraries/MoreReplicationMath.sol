// SPDX-License-Identifier: GPL-3.0-only
pragma solidity >=0.8.6;

import {ABDKMath64x64} from "./ABDKMath64x64.sol";
import {CumulativeNormalDistribution} from "@primitivefi/rmm-core/contracts/libraries/CumulativeNormalDistribution.sol";
import {Units} from "@primitivefi/rmm-core/contracts/libraries/Units.sol";
import {ReplicationMath} from "@primitivefi/rmm-core/contracts/libraries/ReplicationMath.sol";

/**
 * @notice Replication math useful for vaults
 */
library MoreReplicationMath {
    using ABDKMath64x64 for int128;
    using ABDKMath64x64 for uint256;
    using CumulativeNormalDistribution for int128;
    using Units for int128;
    using Units for uint256;

    // https://toolkit.abdk.consulting/math#convert-number
    int128 internal constant ONE_INT = 0x10000000000000000;
    int128 internal constant TWO_INT = 0x20000000000000000;

    /**
     * @notice Compute R1 = 1 - delta = 1 - N(d1). See Primitive whitepaper Sec 3.5
     * @dev Used when creating a new Primitive pool
     * @param spot is the spot price of risky asset in stable
     * @param strike is the strike price of risky asset in stable
     * @param sigma is the implied volatility (normalized)
     * @param tau is the expiry time T minus the current time t
     * @param scaleFactorRisky is the unsigned 256-bit integer scaling factor for risky e.g. 10^(18 - risky.decimals())
     * @param scaleFactorStable is the unsigned 256-bit integer scaling factor for stable e.g. 10^(18 - stable.decimals())
     */
    function getRiskyPerLp(
        uint256 spot,
        uint256 strike,
        uint256 sigma,
        uint256 tau,
        uint256 scaleFactorRisky,
        uint256 scaleFactorStable
    ) internal pure returns (uint256 riskyPerLp) {
        int128 d1;
        {
            /// @dev https://toolkit.abdk.consulting/math#convert-number
            int128 spotX64 = spot.scaleToX64(scaleFactorStable);
            int128 strikeX64 = strike.scaleToX64(scaleFactorStable);
            int128 tauX64 = tau.toYears(); /// @dev: Convert to years
            int128 sqrtTauX64 = tauX64.sqrt();
            int128 sigmaX64 = sigma.percentageToX64();
            int128 sigmaSqrX64 = sigmaX64.pow(2);
            int128 logRatioX64 = spotX64.div(strikeX64).ln();
            int128 crossTermX64 = tauX64.mul(sigmaSqrX64).div(TWO_INT);
            int128 volX64 = sigmaX64.mul(sqrtTauX64);
            d1 = logRatioX64.add(crossTermX64).div(volX64);
        }
        /// @dev: riskyPerLpX64 spans between 0 and 1
        int128 riskyPerLpX64 = ONE_INT.sub(d1.getCDF());

        // return in decimals of the risky asset
        // https://library.primitive.xyz/technical/smart-contracts/autogenerated-docs/manager/PrimitiveManager#create
        riskyPerLp = riskyPerLpX64.scaleFromX64(scaleFactorRisky);
        return riskyPerLp;
    }

    /**
     * @notice Compute stableForLp using Primitive's `ReplicationMath`
     * @dev stablePerLp = K*CDF(CDF^-1(1 - riskyPerLp) - sigma*sqrt(tau)) + invariantLastX64
     * @dev This function is a wrapper around `ReplicatioMath.getStableGivenRisky`
     * @param invariantX64 is the output of `PrimitiveEngine.invariantOf`
     * @param riskyPerLp is the output of `MoreReplicationMath.getRiskyPerLp`
     * @param strike is the strike price of risky asset in stable
     * @param sigma is the implied volatility (normalized)
     * @param tau is the expiry time T minus the current time t
     * @param scaleFactorRisky is the unsigned 256-bit integer scaling factor for risky e.g. 10^(18 - risky.decimals())
     * @param scaleFactorStable is the unsigned 256-bit integer scaling factor for stable e.g. 10^(18 - stable.decimals())
     */
    function getStablePerLp(
        int128 invariantX64,
        uint256 riskyPerLp,
        uint256 strike,
        uint256 sigma,
        uint256 tau,
        uint256 scaleFactorRisky,
        uint256 scaleFactorStable
    ) internal pure returns (uint256 stablePerLp) {
        stablePerLp = ReplicationMath.getStableGivenRisky(
            invariantX64,
            scaleFactorRisky,
            scaleFactorStable,
            riskyPerLp,
            strike,
            sigma,
            tau
        );
        return stablePerLp;
    }
}
